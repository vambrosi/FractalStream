{-# language OverloadedStrings #-}
module Language.Type
  ( FSType(..)
  , type HaskellType
  , Scalar(..)
  , TypeProxy(..)
  , SomeType(..)
  , sameHaskellType
  , Int64
  , Symbol
  , Complex(..)
  , pattern Boolean_
  , pattern Integer_
  , pattern Real_
  , pattern Complex_
  , pattern Rational_
  , pattern Color_
  , showType
  , showValue
  , ppType
  , toUserString
  , KnownType(..)
  , withKnownType
  , withType
  ) where

import FractalStream.Prelude
import Text.Printf

import Data.Color (Color, colorToRGB)

data FSType
  = VoidT
  | BooleanT
  | IntegerT
  | RealT
  | ComplexT
  | RationalT
  | Pair FSType FSType
  | ColorT
  | ImageT
  | ListT FSType
  | TextT
  deriving (Eq, Ord, Show)

-- | Constant values for scalar types
type family HaskellType (t :: FSType) :: Type where
  HaskellType 'BooleanT   = Bool
  HaskellType 'IntegerT   = Int64
  HaskellType 'RealT      = Double
  HaskellType 'ComplexT   = Complex Double
  HaskellType 'RationalT  = (Int64, Int64)
  HaskellType 'ColorT     = Color
  HaskellType ('Pair x y) = (HaskellType x, HaskellType y)
  HaskellType 'VoidT      = ()
  HaskellType 'ImageT     = Int
  HaskellType ('ListT x)  = [HaskellType x]
  HaskellType 'TextT      = String

-- | Constant values for scalar types. Match on the
-- first argument to make the type of the second argument
-- known.
data Scalar (t :: FSType) where
  Scalar :: forall t. TypeProxy t -> HaskellType t -> Scalar t

instance Eq (Scalar t) where
  Scalar t x == Scalar _ y = case t of
    BooleanType  -> x == y
    IntegerType  -> x == y
    RealType     -> x == y
    ComplexType  -> x == y
    RationalType -> x == y
    ColorType    -> x == y
    PairType t1 t2 ->
      let (x1, x2) = x
          (y1, y2) = y
      in (Scalar t1 x1, Scalar t2 x2) == (Scalar t1 y1, Scalar t2 y2)
    VoidType     -> x == y
    ImageType    -> x == y
    ListType it  -> map (Scalar it) x == map (Scalar it) y
    TextType     -> x == y

instance Ord (Scalar t) where
  compare (Scalar t x) (Scalar _ y) = case t of
    BooleanType  -> compare x y
    IntegerType  -> compare x y
    RealType     -> compare x y
    ComplexType  -> compare (realPart x, imagPart x) (realPart y, imagPart y)
    RationalType -> compare x y
    ColorType    -> compare x y
    PairType t1 t2 ->
      let (x1, x2) = x
          (y1, y2) = y
      in compare (Scalar t1 x1, Scalar t2 x2) (Scalar t1 y1, Scalar t2 y2)
    VoidType     -> compare x y
    ImageType    -> compare x y
    ListType it  -> compare (map (Scalar it) x) (map (Scalar it) y)
    TextType     -> compare x y

sameHaskellType :: TypeProxy t1 -> TypeProxy t2 -> Maybe (t1 :~: t2)
sameHaskellType v1 v2 = case v1 of
  BooleanType  -> case v2 of { BooleanType  -> Just Refl; _ -> Nothing }
  IntegerType  -> case v2 of { IntegerType  -> Just Refl; _ -> Nothing }
  RealType     -> case v2 of { RealType     -> Just Refl; _ -> Nothing }
  ComplexType  -> case v2 of { ComplexType  -> Just Refl; _ -> Nothing }
  RationalType -> case v2 of { RationalType -> Just Refl; _ -> Nothing }
  ColorType    -> case v2 of { ColorType    -> Just Refl; _ -> Nothing }
  VoidType     -> case v2 of { VoidType     -> Just Refl; _ -> Nothing }
  ImageType    -> case v2 of { ImageType    -> Just Refl; _ -> Nothing }
  TextType     -> case v2 of { TextType     -> Just Refl; _ -> Nothing }
  PairType x y -> case v2 of
    PairType x' y' -> case (,) <$> sameHaskellType x x' <*> sameHaskellType y y' of
      Just (Refl, Refl) -> Just Refl
      Nothing           -> Nothing
    _ -> Nothing
  ListType x -> case v2 of
    ListType x' -> case sameHaskellType x x' of
      Just Refl -> Just Refl
      Nothing   -> Nothing
    _ -> Nothing

-- | Singleton values reflecting the type-level parameter @t@.
data TypeProxy (t :: FSType) where
  BooleanType  :: TypeProxy 'BooleanT
  IntegerType  :: TypeProxy 'IntegerT
  RealType     :: TypeProxy 'RealT
  ComplexType  :: TypeProxy 'ComplexT
  RationalType :: TypeProxy 'RationalT
  ColorType    :: TypeProxy 'ColorT
  PairType     :: (KnownType x, KnownType y) => TypeProxy x -> TypeProxy y -> TypeProxy ('Pair x y)
  VoidType     :: TypeProxy 'VoidT
  ImageType    :: TypeProxy 'ImageT
  ListType     :: KnownType x => TypeProxy x -> TypeProxy ('ListT x)
  TextType     :: TypeProxy 'TextT

data SomeType where
  SomeType :: forall t. TypeProxy t -> SomeType

instance Show SomeType where
  show (SomeType t) = showType t

instance Eq SomeType where
  SomeType t1 == SomeType t2 = maybe False (const True) (sameHaskellType t1 t2)

class KnownType (t :: FSType)   where typeProxy :: TypeProxy t
instance KnownType 'BooleanT  where typeProxy = BooleanType
instance KnownType 'IntegerT  where typeProxy = IntegerType
instance KnownType 'RealT     where typeProxy = RealType
instance KnownType 'ComplexT  where typeProxy = ComplexType
instance KnownType 'RationalT where typeProxy = RationalType
instance KnownType 'ColorT    where typeProxy = ColorType
instance KnownType 'VoidT     where typeProxy = VoidType
instance KnownType 'ImageT    where typeProxy = ImageType
instance KnownType 'TextT     where typeProxy = TextType
instance KnownType x => KnownType ('ListT x) where
  typeProxy = ListType (typeProxy @x)
instance (KnownType x, KnownType y) => KnownType ('Pair x y) where
  typeProxy = PairType (typeProxy @x) (typeProxy @y)

withKnownType :: TypeProxy t -> (KnownType t => a) -> a
withKnownType ty k = case ty of
  BooleanType  -> k
  IntegerType  -> k
  RealType     -> k
  ComplexType  -> k
  RationalType -> k
  ColorType    -> k
  VoidType     -> k
  ImageType    -> k
  TextType     -> k
  PairType {}  -> k
  ListType {}  -> k

withType :: forall a. FSType -> (forall ty. KnownType ty => TypeProxy ty -> a) -> a
withType t k = case t of
  VoidT    -> k VoidType
  BooleanT -> k BooleanType
  IntegerT -> k IntegerType
  RealT    -> k RealType
  ComplexT -> k ComplexType
  ColorT   -> k ColorType
  ImageT   -> k ImageType
  TextT    -> k TextType
  ListT lt -> withType lt $ \ty -> k (ListType ty)
  RationalT  -> k RationalType
  Pair t1 t2 -> withType t1 $ \ty1 -> withType t2 $ \ty2 -> k (PairType ty1 ty2)


pattern Boolean_ :: forall (t :: FSType). () => (t ~ 'BooleanT) => HaskellType t -> Scalar t
pattern Boolean_ x = Scalar BooleanType x

pattern Integer_ :: forall (t :: FSType). () => (t ~ 'IntegerT) => HaskellType t -> Scalar t
pattern Integer_ x = Scalar IntegerType x

pattern Real_ :: forall (t :: FSType). () => (t ~ 'RealT) => HaskellType t -> Scalar t
pattern Real_ x    = Scalar RealType x

pattern Complex_ :: forall (t :: FSType). () => (t ~ 'ComplexT) => HaskellType t -> Scalar t
pattern Complex_  pair = Scalar ComplexType pair

pattern Rational_ :: forall (t :: FSType). () => (t ~ 'RationalT) => HaskellType t -> Scalar t
pattern Rational_ pair = Scalar RationalType pair

pattern Color_ :: forall (t :: FSType). () => (t ~ 'ColorT) => HaskellType t -> Scalar t
pattern Color_ c = Scalar ColorType c

showType :: TypeProxy t -> String
showType = \case
  BooleanType  -> "truth value"
  IntegerType  -> "integer"
  RealType     -> "real number"
  ComplexType  -> "complex number"
  RationalType -> "rational number"
  ColorType    -> "color"
  VoidType     -> "unit"
  ImageType    -> "image"
  TextType     -> "text"
  PairType x y -> "(" <> showType x <> " x " <> showType y <> ")"
  ListType x   -> "list of " <> showType x

ppType :: TypeProxy t -> String
ppType = \case
  BooleanType  -> "Boolean"
  IntegerType  -> "‚Ñ§"
  RealType     -> "‚Ñù"
  ComplexType  -> "‚ÑÇ"
  RationalType -> "Rational"
  ColorType    -> "Color"
  VoidType     -> "Unit"
  ImageType    -> "Image"
  TextType     -> "Text"
  PairType{}   -> error "TODO"
  ListType t   -> "List of " ++ ppType t

showValue :: TypeProxy t -> HaskellType t -> String
showValue ty v = case ty of
  BooleanType  -> if v then "true" else "false"
  IntegerType  -> show v
  RealType     -> show v
  ComplexType  -> let x :+ y = v
                   in show x <> " + " <> show y <> "i"
  RationalType -> let (x, y) = v
                   in show x <> " / " <> show y
  ColorType    -> show (colorToRGB v)
  VoidType     -> "n/a"
  ImageType    -> "(image)"
  TextType     -> v
  PairType xt yt -> let (x, y) = v
                     in showValue xt x <> " , " <> showValue yt y
  ListType xt -> "[" ++ intercalate ", " (map (showValue xt) v) ++ "]"

instance An SomeType where
  an = an . show

instance KnownType t => An (TypeProxy t) where
  an = an . SomeType

toUserString :: TypeProxy t -> HaskellType t -> String
toUserString ty0 = tidy . go ty0
  where
    tidy :: String -> String
    tidy s = case ty0 of
      ListType _ -> reverse . drop 1 . reverse . drop 1 $ s
      _ -> s

    go :: TypeProxy ty -> HaskellType ty -> String
    go ty v = case ty of
      RealType -> printf "%0.14f" v
      ComplexType -> let x :+ y = v
                     in if y == 0 then go RealType x
                        else if x == 0 then go RealType y ++ "ùëñ"
                             else if y <0 then printf "%0.14f - %0.14fùëñ" x (-y)
                                  else printf "%0.14f + %0.14fùëñ" x y
      ColorType -> let (r,g,b) = colorToRGB v
                   in printf "rgb(%0.3f, %0.3f, %0.3f)"
                      (fromIntegral r / 255.0 :: Double)
                      (fromIntegral g / 255.0 :: Double)
                      (fromIntegral b / 255.0 :: Double)
      PairType ty1 ty2 -> concat ["(", go ty1 (fst v), ", ", go ty2 (snd v), ")"]
      ListType ity -> "[" ++ intercalate ", " (map (go ity) v) ++ "]"
      _ -> showValue ty v
